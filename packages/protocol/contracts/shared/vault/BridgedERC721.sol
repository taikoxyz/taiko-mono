// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "../common/EssentialContract.sol";
import "../libs/LibNames.sol";
import "./IBridgedERC721.sol";
import "./LibBridgedToken.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/ERC721Upgradeable.sol";

/// @title BridgedERC721
/// @notice Contract for bridging ERC721 tokens across different chains.
/// @custom:security-contact security@taiko.xyz
contract BridgedERC721 is
    EssentialContract,
    IBridgedERC721,
    IBridgedERC721Initializable,
    ERC721Upgradeable
{
    address public immutable erc721Vault;

    /// @notice Address of the source token contract.
    address public srcToken;

    /// @notice Source chain ID where the token originates.
    uint256 public srcChainId;

    uint256[48] private __gap;

    error BTOKEN_INVALID_BURN();

    constructor(address _erc721Vault) {
        erc721Vault = _erc721Vault;
    }

    /// @inheritdoc IBridgedERC721Initializable
    function init(
        address _owner,
        address _srcToken,
        uint256 _srcChainId,
        string calldata _symbol,
        string calldata _name
    )
        external
        initializer
    {
        // Check if provided parameters are valid
        LibBridgedToken.validateInputs(_srcToken, _srcChainId);
        __Essential_init(_owner);
        __ERC721_init(_name, _symbol);

        srcToken = _srcToken;
        srcChainId = _srcChainId;
    }

    /// @inheritdoc IBridgedERC721
    function mint(
        address _account,
        uint256 _tokenId
    )
        external
        whenNotPaused
        onlyFrom(erc721Vault)
        nonReentrant
    {
        _safeMint(_account, _tokenId);
    }

    /// @inheritdoc IBridgedERC721
    function burn(uint256 _tokenId) external whenNotPaused onlyFrom(erc721Vault) nonReentrant {
        // Check if the caller is the owner of the token. Somehow this is not done inside the
        // _burn() function below.
        if (ownerOf(_tokenId) != msg.sender) {
            revert BTOKEN_INVALID_BURN();
        }
        _burn(_tokenId);
    }

    /// @inheritdoc IBridgedERC721
    function canonical() external view returns (address, uint256) {
        return (srcToken, srcChainId);
    }

    /// @notice Returns the token URI.
    /// @param _tokenId The token id.
    /// @return The token URI following EIP-681.
    function tokenURI(uint256 _tokenId) public view override returns (string memory) {
        // https://github.com/crytic/slither/wiki/Detector-Documentation#abi-encodePacked-collision
        // The abi.encodePacked() call below takes multiple dynamic arguments. This is known and
        // considered acceptable in terms of risk.
        return LibBridgedToken.buildURI(srcToken, srcChainId, Strings.toString(_tokenId));
    }

    function supportsInterface(bytes4 _interfaceId) public view override returns (bool) {
        return _interfaceId == type(IBridgedERC721).interfaceId
            || _interfaceId == type(IBridgedERC721Initializable).interfaceId
            || super.supportsInterface(_interfaceId);
    }

    function _beforeTokenTransfer(
        address _from,
        address _to,
        uint256 _firstTokenId,
        uint256 _numBlocks
    )
        internal
        override
        whenNotPaused
    {
        LibBridgedToken.checkToAddress(_to);
        super._beforeTokenTransfer(_from, _to, _firstTokenId, _numBlocks);
    }
}




// ===== STORAGE LAYOUT START =====
// This section is auto-generated by script/gen-layouts.sh. Do not edit manually.
//
// 
// ╭-----------------------------+----------------------------------------------+------+--------+-------+--------------------------------------------------------╮
// | Name                        | Type                                         | Slot | Offset | Bytes |
// +=============================================================================================================================================================+
// | _initialized                | uint8                                        | 0    | 0      | 1     |
// |
// | _initializing               | bool                                         | 0    | 1      | 1     |
// |
// | __gap                       | uint256[50]                                  | 1    | 0      | 1600  |
// |
// | _owner                      | address                                      | 51   | 0      | 20    |
// |
// | __gap                       | uint256[49]                                  | 52   | 0      | 1568  |
// |
// | _pendingOwner               | address                                      | 101  | 0      | 20    |
// |
// | __gap                       | uint256[49]                                  | 102  | 0      | 1568  |
// |
// | __gapFromOldAddressResolver | uint256[50]                                  | 151  | 0      | 1600  |
// |
// | __reentry                   | uint8                                        | 201  | 0      | 1     |
// |
// | __paused                    | uint8                                        | 201  | 1      | 1     |
// |
// | __gap                       | uint256[49]                                  | 202  | 0      | 1568  |
// |
// | __gap                       | uint256[50]                                  | 251  | 0      | 1600  |
// |
// | _name                       | string                                       | 301  | 0      | 32    |
// |
// | _symbol                     | string                                       | 302  | 0      | 32    |
// |
// | _owners                     | mapping(uint256 => address)                  | 303  | 0      | 32    |
// |
// | _balances                   | mapping(address => uint256)                  | 304  | 0      | 32    |
// |
// | _tokenApprovals             | mapping(uint256 => address)                  | 305  | 0      | 32    |
// |
// | _operatorApprovals          | mapping(address => mapping(address => bool)) | 306  | 0      | 32    |
// |
// | __gap                       | uint256[44]                                  | 307  | 0      | 1408  |
// |
// | srcToken                    | address                                      | 351  | 0      | 20    |
// |
// | srcChainId                  | uint256                                      | 352  | 0      | 32    |
// |
// | __gap                       | uint256[48]                                  | 353  | 0      | 1536  |
// ╰-----------------------------+----------------------------------------------+------+--------+-------+--------------------------------------------------------╯
// 
//
// ===== STORAGE LAYOUT END =====
