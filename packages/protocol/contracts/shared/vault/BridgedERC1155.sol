// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import "../common/EssentialContract.sol";
import "../libs/LibNames.sol";
import "./IBridgedERC1155.sol";
import "./LibBridgedToken.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC1155/ERC1155Upgradeable.sol";

/// @title BridgedERC1155
/// @notice Contract for bridging ERC1155 tokens across different chains.
/// @custom:security-contact security@taiko.xyz
contract BridgedERC1155 is
    EssentialContract,
    IBridgedERC1155,
    IBridgedERC1155Initializable,
    ERC1155Upgradeable
{
    address public immutable erc1155Vault;

    /// @notice Address of the source token contract.
    address public srcToken;

    /// @notice Source chain ID where the token originates.
    uint256 public srcChainId;

    /// @dev Symbol of the bridged token.
    string public symbol;

    /// @dev Name of the bridged token.
    string public name;

    uint256[46] private __gap;

    error BTOKEN_INVALID_PARAMS();

    constructor(address _erc1155Vault) {
        erc1155Vault = _erc1155Vault;
    }

    /// @inheritdoc IBridgedERC1155Initializable
    function init(
        address _owner,
        address _srcToken,
        uint256 _srcChainId,
        string calldata _symbol,
        string calldata _name
    )
        external
        initializer
    {
        // Check if provided parameters are valid.
        // The symbol and the name can be empty for ERC1155 tokens so we use some placeholder data
        // for them instead.
        LibBridgedToken.validateInputs(_srcToken, _srcChainId);
        __Essential_init(_owner);

        // The token URI here is not important as the client will have to read the URI from the
        // canonical contract to fetch meta data.
        __ERC1155_init(LibBridgedToken.buildURI(_srcToken, _srcChainId, ""));

        srcToken = _srcToken;
        srcChainId = _srcChainId;
        symbol = _symbol;
        name = _name;
    }

    /// @inheritdoc IBridgedERC1155
    function mintBatch(
        address _to,
        uint256[] calldata _tokenIds,
        uint256[] calldata _amounts
    )
        external
        whenNotPaused
        onlyFrom(erc1155Vault)
        nonReentrant
    {
        _mintBatch(_to, _tokenIds, _amounts, "");
    }

    /// @inheritdoc IBridgedERC1155
    function burn(
        uint256 _id,
        uint256 _amount
    )
        external
        whenNotPaused
        onlyFrom(erc1155Vault)
        nonReentrant
    {
        _burn(msg.sender, _id, _amount);
    }

    /// @inheritdoc IBridgedERC1155
    function canonical() external view returns (address, uint256) {
        return (srcToken, srcChainId);
    }

    function supportsInterface(bytes4 _interfaceId) public view override returns (bool) {
        return _interfaceId == type(IBridgedERC1155).interfaceId
            || _interfaceId == type(IBridgedERC1155Initializable).interfaceId
            || super.supportsInterface(_interfaceId);
    }

    function _beforeTokenTransfer(
        address _operator,
        address _from,
        address _to,
        uint256[] memory _ids,
        uint256[] memory _amounts,
        bytes memory _data
    )
        internal
        override
        whenNotPaused
    {
        LibBridgedToken.checkToAddress(_to);
        super._beforeTokenTransfer(_operator, _from, _to, _ids, _amounts, _data);
    }
}



// ===== STORAGE LAYOUT START =====
// This section is auto-generated by script/gen-layouts.sh. Do not edit manually.
//
// 
// ╭-----------------------------+-------------------------------------------------+------+--------+-------+----------------------------------------------------------╮
// | Name                        | Type                                            | Slot | Offset | Bytes |
// +==================================================================================================================================================================+
// | _initialized                | uint8                                           | 0    | 0      | 1     |
// |
// | _initializing               | bool                                            | 0    | 1      | 1     |
// |
// | __gap                       | uint256[50]                                     | 1    | 0      | 1600  |
// |
// | _owner                      | address                                         | 51   | 0      | 20    |
// |
// | __gap                       | uint256[49]                                     | 52   | 0      | 1568  |
// |
// | _pendingOwner               | address                                         | 101  | 0      | 20    |
// |
// | __gap                       | uint256[49]                                     | 102  | 0      | 1568  |
// |
// | __gapFromOldAddressResolver | uint256[50]                                     | 151  | 0      | 1600  |
// |
// | __reentry                   | uint8                                           | 201  | 0      | 1     |
// |
// | __paused                    | uint8                                           | 201  | 1      | 1     |
// |
// | __gap                       | uint256[49]                                     | 202  | 0      | 1568  |
// |
// | __gap                       | uint256[50]                                     | 251  | 0      | 1600  |
// |
// | _balances                   | mapping(uint256 => mapping(address => uint256)) | 301  | 0      | 32    |
// |
// | _operatorApprovals          | mapping(address => mapping(address => bool))    | 302  | 0      | 32    |
// |
// | _uri                        | string                                          | 303  | 0      | 32    |
// |
// | __gap                       | uint256[47]                                     | 304  | 0      | 1504  |
// |
// | srcToken                    | address                                         | 351  | 0      | 20    |
// |
// | srcChainId                  | uint256                                         | 352  | 0      | 32    |
// |
// | symbol                      | string                                          | 353  | 0      | 32    |
// |
// | name                        | string                                          | 354  | 0      | 32    |
// |
// | __gap                       | uint256[46]                                     | 355  | 0      | 1472  |
// ╰-----------------------------+-------------------------------------------------+------+--------+-------+----------------------------------------------------------╯
// 
//
// ===== STORAGE LAYOUT END =====
